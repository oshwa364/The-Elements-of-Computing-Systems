// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    Not16(in=x, out=invertX);
    And16(a=x, b=invertX, out=zeroX);
    Not16(in=zeroX, out=fullOneX);
    Mux16(a=x, b=zeroX, sel=zx, out=outx1);
    Mux16(a=invertX, b=fullOneX, sel=zx, out=outx2);
    Mux16(a=outx1, b=outx2, sel=nx, out=goodX);

    Not16(in=y, out=invertY);
    And16(a=y, b=invertY, out=zeroY);
    Not16(in=zeroY, out=fullOneY);
    Mux16(a=y, b=zeroY, sel=zy, out=outy1);
    Mux16(a=invertY, b=fullOneY, sel=zy, out=outy2);
    Mux16(a=outy1, b=outy2, sel=ny, out=goodY);

    Add16(a=goodX, b=goodY, out=sumXY);
    And16(a=goodX, b=goodY, out=multXY);

    Mux16(a=multXY, b=sumXY, sel=f, out=almostOut);

    Not16(in=almostOut, out=invertAlmostOut);
    Mux16(a=almostOut, b=invertAlmostOut, sel=no, out=out);

    Mux16(a=almostOut, b=invertAlmostOut, sel=no,
            out[0..7]=out1, out[8..15]=out2, out[15]=ngBit);
    Or8Way(in=out1, out=outOr1);
    Or8Way(in=out2, out=outOr2);
    Or(a=outOr1, b=outOr2, out=almostZr);
    Not(in=almostZr, out=zr);

    Or(a=ngBit, b=false, out=ng);
}